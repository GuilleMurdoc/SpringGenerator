-- @nsURI UML=http://www.eclipse.org/uml2/5.0.0/UML
-- @path SpringMetamodel=/MetamodelSpringGenerator/model/metamodelSpringGenerator.ecore

module SpringProfile2SpringMetamodel;
create OUT : SpringMetamodel from IN : UML;

rule model2app {
	from
		model : UML!Model
	to 
		app : SpringMetamodel!App (
			name <- model.name,
			basePackage <- thisModule.getBasePackage(model.packagedElement).name,
			dtos <- model.allStereotypedClasses('DTO'),
			controllers <- model.allStereotypedClasses('Controller')->collect(ctrClass | thisModule.controllerClass2controller(ctrClass))
		)
}

rule dtoClass2dto {
	from
		dtoClass : UML!Class (not dtoClass.getStereotype('DTO').oclIsUndefined())
	to 
		dto : SpringMetamodel!DTO (
			name <- dtoClass.name.concat('DTO'),
			attributes <- dtoClass.getAllAttributes()->collect(attr | thisModule.property2attribute(attr)),
			package <- thisModule.getPackageString(thisModule.reverse(dtoClass.allPackages()->select(p | not p.isBase())))
		)
}

lazy rule controllerClass2controller {
	from
		controllerClass : UML!Class (not controllerClass.getStereotype('Controller').oclIsUndefined())
	to 
		controller : SpringMetamodel!Controller (
			name <- controllerClass.name,
			pathparts <- controllerClass.getValue(controllerClass.getStereotype('Controller'), 'path')->collect(pp | thisModule.pathPartMap2pathPart(pp)),
			endpoints <- controllerClass.getAllOperations()->collect(op | thisModule.operation2endpoint(op))
		)
}

lazy rule property2attribute {
	from
		property : UML!Property
	to 
		attribute : SpringMetamodel!Attribute (
			name <- property.name,
			attributetype <- if property.upper = -1
				then thisModule.property2listAttributeType(property)
				else thisModule.property2simpleAttributeType(property) endif
		)
}

lazy rule operation2endpoint {
	from
		operation : UML!Operation
	to 
		entryEndpoint : SpringMetamodel!Endpoint (
			name <- operation.name,
			pathparts <- operation.getValue(operation.getStereotype('Endpoint'), 'path')->collect(pp | thisModule.pathPartMap2pathPart(pp)), --TODO remove path on Controller and put all together in Endpoint
			method <- operation.getValue(operation.getStereotype('Endpoint'), 'httpMethod').name,
			inputBody <- let op : UML!Operation = operation.ownedParameter->select(p | p.direction.toString() = 'in')->first() in
				if not op.oclIsUndefined() then op.type else OclUndefined endif,
			outputBody <- let op : UML!Operation = operation.ownedParameter->select(p | p.direction.toString() = 'return')->first() in
				if not op.oclIsUndefined() then op.type else OclUndefined endif,
			outputIsList <- let retParam : UML!Parameter = operation.ownedParameter->select(p | p.direction.toString() = 'return')->first() in
				if not retParam.oclIsUndefined() then retParam.upper = -1 else false
     endif
		)
}

lazy rule pathPartMap2pathPart {
	from
		pathPartMap : UML!Element
	to 
		pathPart : SpringMetamodel!PathPart (
			value <- pathPartMap.name,
			variableType <- if pathPartMap.variable then pathPartMap.variableType.name else OclUndefined endif
		)
}

-- AttributeType --

lazy rule property2simpleAttributeType {
	from
		property : UML!Property
	to 
		attributeType : SpringMetamodel!SimpleAttributeType (
			type <- thisModule.property2type(property),
			nullable <- property.lower = 0
		)
}

lazy rule property2listAttributeType {
	from
		property : UML!Property
	to 
		attributeType : SpringMetamodel!ListAttributeType (
			type <- thisModule.property2type(property)
		)
}

-- AttributeType --

lazy rule property2type {
	from
		property : UML!Property
	to 
		springType : SpringMetamodel!Type (
			dto <- if not property.association.oclIsUndefined()
				then property.type
				else OclUndefined
			endif,
			primitiveType <- if property.association.oclIsUndefined()
				then thisModule.umlTypeToPrimitiveType(property.type.name)
				else OclUndefined
			endif
		)
}

helper def : umlTypeToPrimitiveType(umlType : String) : String =
	let type : String = umlType.toLower() in
	if type.indexOf('int') > -1  or type.indexOf('long') > -1
		then 'Integer'
	else
	if type.indexOf('double') > -1 or type.indexOf('float') > -1
		then 'Double'
	else 'String' endif endif;


-- allPackages --

helper context UML!Class def: allPackages() : Sequence(UML!Package) =
    if self.package.oclIsUndefined() then
        Sequence{}
    else
        self.package.allPackages()->prepend(self.package)
    endif;

helper context UML!Package def: allPackages() : Sequence(UML!Package) =
    if self.isBase() then
        Sequence{}
    else
        self.nestingPackage.allPackages()->prepend(self.nestingPackage)
    endif;

-- allPackages --
-- allClasses --

helper context UML!Model def : allStereotypedClasses(stereotypeName : String) : Sequence(UML!Class) =
    self.allClasses()->select(class | not class.getStereotype(stereotypeName).oclIsUndefined());

helper context UML!Model def : allClasses() : Sequence(UML!Class) =
    let directClasses : Sequence(UML!Class) = self.packagedElement->select(e | e.oclIsKindOf(UML!Class)) in
    let nestedClasses : Sequence(UML!Class) =
        self.packagedElement->select(e | e.oclIsKindOf(UML!Package))
        ->collect(e | let p : UML!Package = e in p.allClasses())
        ->flatten()
    in
    directClasses->union(nestedClasses);
    
helper context UML!Package def : allClasses() : Sequence(UML!Class) =
    let directClasses : Sequence(UML!Class) = self.packagedElement->select(e | e.oclIsKindOf(UML!Class)) in
    let nestedClasses : Sequence(UML!Class) =
        self.packagedElement->select(e | e.oclIsKindOf(UML!Package))
        ->collect(e | let p : UML!Package = e in p.allClasses())
        ->flatten()
    in
    directClasses->union(nestedClasses);
    
-- allClasses --

helper context UML!Package def: isBase() : Boolean =
  let dtoPkg : UML!Stereotype = self.getStereotype('DtoPackage') in
  let controllerPkg : UML!Stereotype = self.getStereotype('ControllerPackage') in
    if not dtoPkg.oclIsUndefined() then self.getValue(dtoPkg, 'base')
    else if not controllerPkg.oclIsUndefined() then self.getValue(controllerPkg, 'base')
    else false
    endif
	endif;

helper def : getBasePackage(elements : Sequence(OclAny)) : UML!Package =
	elements->select(e | e.oclIsKindOf(UML!Package))->select(p | p.isBase())->first();

helper def : getPackageString(packages : Sequence(UML!Package)) : String =
	packages->collect(p | p.name)->iterate(p; pString: String = '' |
		if pString = '' then p else pString.concat('.').concat(p) endif
);

helper def : reverse(seq : Sequence(OclAny)) : Sequence(OclAny) =
    seq->iterate(elem; rev : Sequence(OclAny) = Sequence{} |
        rev->prepend(elem));

helper context UML!Element def : getStereotype(name : String) : UML!Stereotype = self.getAppliedStereotypes()->select(s | s.qualifiedName = 'ProfileSpringGenerator::'.concat(name))->first();
